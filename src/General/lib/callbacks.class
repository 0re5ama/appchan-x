class Callbacks
  constructor: (@type) ->
    @keys = []

  push: ({name, cb}) ->
    @rm name if @[name]

    @[name] = cb
    @keys.push name

  clean: ->
    @rm name for name in @keys
    return

  rm: (name) ->
    return unless @[name]
    delete @[name]
    @keys.splice @keys.indexOf(name), 1 # Didn't I make this structure to avoid this? ;__;

  execute: (node) ->
    for name in @keys
      try
        @[name].call node
      catch err
        errors = [] unless errors
        errors.push
          message: ['"', name, '" crashed on node ', @type, ' No.', node, ' (', node.board, ').'].join('')
          error: err

    Main.handleErrors errors if errors
