class Callbacks
  constructor: (@type) ->
    @keys = []

  push: ({name, cb}) ->
    if @[name]
      @connect name
    else
      @keys.push name
    @[name] = cb

  connect:    (name) -> delete @[name].disconnected if @[name].disconnected
  disconnect: (name) -> @[name].disconnected = true if @[name]

  execute: (nodes) ->
    for name in @keys
      feature = @[name]
      for node in nodes when not feature.disconnected
        try
          feature.call node 
        catch err
          errors = [] unless errors
          errors.push
            message: ['"', name, '" crashed on node ', @type, ' No.', node.ID, ' (', node.board, ').'].join('')
            error: err

    Main.handleErrors errors if errors
