class Callbacks
  constructor: (@type) ->
    @keys = []

  push: ({name, cb}) ->
    @connect name if @[name]
    @keys.push name unless @[name]
    @[name] = cb

  connect:    (name) -> delete @[name].disconnected if @[name].disconnected
  disconnect: (name) -> @[name].disconnected = true if @[name]

  execute: (nodes) ->
    i = 0
    # c.time 'Features'
    while name = @keys[i++]
      j = 0
      cb = @[name]
      # c.time name
      while node = nodes[j++]
        try
          cb.call node unless cb.disconnected
        catch err
          errors = [] unless errors
          errors.push
            message: ['"', name, '" crashed on node ', @type, ' No.', node.ID, ' (', node.board, ').'].join('')
            error: err
      # c.timeEnd name
    # c.timeEnd 'Features'

    Main.handleErrors errors if errors
