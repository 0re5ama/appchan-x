class Callbacks
  constructor: (@type) ->
    @keys = []

  push: ({name, cb}) ->
    @[name].connect() if @[name]
    @keys.push name unless @[name]
    @[name] = cb

  clean: ->
    @rm name for name in @keys
    return

  connect:    (name) -> delete @[name].disconnected if @[name]
  disconnect: (name) -> @[name].disconnected = true if @[name]

  execute: (node) ->
    for name in @keys
      try
        @[name].call node unless @[name].disconnected
      catch err
        errors = [] unless errors
        errors.push
          message: ['"', name, '" crashed on node ', @type, ' No.', node, ' (', node.board, ').'].join('')
          error: err

    Main.handleErrors errors if errors
